import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("com.android.application")
    id("com.google.devtools.ksp")
    id("com.google.firebase.crashlytics")
    id("com.google.gms.google-services")
    id("org.jetbrains.kotlin.android")
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "ru.yourok.torrserve"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
        // for older androids
        multiDexEnabled true
        versionCode 123
        versionName "MatriX.123.Client"
        setProperty("archivesBaseName", "TorrServe_" + versionName)
        // Use SupportLibrary for vectors
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    // kotlin-kapt workaround for kotlin_version 1.8.0
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }
    namespace 'ru.yourok.torrserve'
}

dependencies {
    // multidex
    implementation 'androidx.multidex:multidex:2.0.1'
    // SSL
    implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1')
    // http client/parser
    implementation 'org.jsoup:jsoup:1.16.1'
    // json serializer
    //noinspection GradleDependency 2.10.x thorow java.lang.VerifyError on api17
    implementation 'com.google.code.gson:gson:2.9.1' // 2.10.1
    // root shell (3.2.1)
    //noinspection GradleDependency 3.2.1 last api17 compatible
    implementation 'com.github.topjohnwu.libsu:core:3.2.1'
    // glide
    def glideVersion = '4.15.1'
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    ksp "com.github.bumptech.glide:ksp:${glideVersion}"
    // firebase
    //noinspection GradleDependency firebase-core:19.0.2 is last api17 compatible
    implementation 'com.google.firebase:firebase-core:19.0.2'
    //noinspection GradleDependency firebase-analytics:19.0.2 is last api17 compatible
    implementation 'com.google.firebase:firebase-analytics:19.0.2'
    //noinspection GradleDependency firebase-crashlytics:18.2.5 is last api17 compatible
    implementation 'com.google.firebase:firebase-crashlytics:18.2.5'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}